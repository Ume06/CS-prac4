// File name: Jump.hdl

/**
 * The jump unit determines whether the Program Counter should jump.
 * A jump is performed if any of the j1 j2 j3 bits of the c-instruction 
 * are true, AND the matching condition is met:
 *   j1: jump if ALU output is < 0
 *   j2: jump if ALU output   == 0
 *   j3: jump if ALU output is > 0
 * If multiple of these bits are set true, then jump if any of 
 * those conditions are met,
 * e.g. if j1 and j2 are both set true, jump if < 0 or == 0 
 * The ALU output is determined through the zr and ng wires:
 *   zr: is true when the ALU's output   == 0, false otherwise
 *   ng: is true when the ALU's output is < 0, false otherwise
 * If both zr and ng are false, then the ALU's output is > 0
 */

CHIP Jump {

    IN  j1,             // c-instruction j1
        j2,             // c-instruction j2
        j3,             // c-instruction j3
        zr,             // zr pin from ALU
        ng;             // ng pin from ALU

    OUT jump;           // true if a jump should be performed, false otherwise 

PARTS:
    // Put your code here:
    // Check if ALU output is < 0 (ng is true)
    And(a=j1, b=ng, out=j1ng);
    
    // Check if ALU output == 0 (zr is true)
    And(a=j2, b=zr, out=j2zr);

    // Check if ALU output > 0 (ng and zr are both false)
    Not(in=ng, out=ngNot);
    Not(in=zr, out=zrNot);
    And(a=ngNot, b=zrNot, out=gt0);
    And(a=j3, b=gt0, out=j3gt0);

    // Combine the results from the three cases and output the final jump signal
    Or(a=j1ng, b=j2zr, out=j1j2);
    Or(a=j1j2, b=j3gt0, out=jump);
}